plugins {
    id 'fabric-loom' version '1.9-SNAPSHOT'
    id 'com.palantir.git-version' version '3.1.0'
}

group = project.maven_group
version = "0.0.1-SNAPSHOT"

base {
    archivesName = project.archives_base_name
}

loom {
    mods {
        "meh" {
            sourceSet sourceSets.main
        }
    }
}

repositories {
    maven { url "https://maven.shedaniel.me/" } // Cloth Config API
    maven { url "https://maven.terraformersmc.com/releases/" } // Mod Menu
    maven { url "https://repo.hypixel.net/repository/Hypixel/" } // Hypixel Mod API
    // maven { url "https://maven.siphalor.de/" } // AMECS (keybinds)
}

dependencies {
    mappings loom.officialMojangMappings()

    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    modApi ("me.shedaniel.cloth:cloth-config-fabric:${project.cloth_version}") { // Cloth Version found at https://linkie.shedaniel.dev/dependencies
        exclude(group: "net.fabricmc.fabric-api")
    }
    modApi "com.terraformersmc:modmenu:${project.mod_menu_version}"
    modApi "net.hypixel:mod-api:${project.hypixel_mod_api_version}"
    // modApi "de.siphalor:amecsapi-1.20:1.5.6+mc1.20.2" // Amecs API provided by "too many shortcuts": https://github.com/wyatt-herkamp/too-many-shortcuts
}

processResources {
    outputs.upToDateWhen { false } // Always process resource properties

    def details = versionDetails()
    def archiveVersion = "${details.gitHash}-${details.branchName}"
    inputs.property "version", archiveVersion

    filesMatching("fabric.mod.json") {
        expand "version": archiveVersion
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 21
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.base.archivesName.get()}"}
    }
}
